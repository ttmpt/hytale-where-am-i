import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'java'
}

ext {
    hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))
    compileOnly(files("libs/MultipleHUD-1.0.1.jar"))
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

processResources {
    doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: ['**/*.json', '**/*.mcmeta']).each {
            try {
                def oldLength = it.length()
                it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
                jsonBytesSaved += oldLength - it.length()
                jsonMinified++
            }
            catch (Exception e) {
                project.logger.error("Failed to minify file '${it.path}'.")
                throw e
            }
        }
        project.logger.lifecycle("Minified ${jsonMinified} files. Saved ${jsonBytesSaved} bytes before compression. Took ${System.currentTimeMillis() - jsonMinifyStart}ms.")
    }
}
